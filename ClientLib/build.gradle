apply plugin: 'com.android.library'

ext {
    VERSION_MAJOR = 2
    VERSION_MINOR = 7
    VERSION_PATCH = 0
    VERSION_SUFFIX = "release"

    PUBLISH_ARTIFACT_ID = 'dronekit-android'
    PUBLISH_VERSION = generateVersionName("", VERSION_MAJOR, VERSION_MINOR, VERSION_PATCH, VERSION_SUFFIX)
    PUBLISH_VERSION_CODE = computeVersionCode(VERSION_MAJOR, VERSION_MINOR, VERSION_PATCH)
    PROJECT_DESCRIPTION = "Android DroneKit client library."
    PROJECT_LABELS = ['3DR', '3DR Services', 'DroneAPI', 'Android', 'DroneKit']
    PROJECT_LICENSES = ['Apache-2.0']
}

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.2'

    defaultConfig {
        minSdkVersion Integer.parseInt(project.ANDROID_BUILD_MIN_SDK_VERSION)
        targetSdkVersion Integer.parseInt(project.ANDROID_BUILD_TARGET_SDK_VERSION)
        versionCode PUBLISH_VERSION_CODE
        versionName PUBLISH_VERSION
        multiDexEnabled true
    }

    sourceSets {
        main {
            aidl.srcDirs = ['src/main/java']
        }
        main.java.srcDirs += 'src/main/kotlin'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    lintOptions {
        abortOnError false
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    android.libraryVariants.all { variant ->
        variant.outputs.each { output ->
            def file = output.outputFile
            if (file != null && file.name.endsWith('.aar')) {
                output.outputFile = new File(file.parent, "${PUBLISH_ARTIFACT_ID}.${PUBLISH_VERSION}.aar")
            }
        }
    }
}

dependencies {
    compile 'com.android.support:support-v4:23.2.1'
    compile 'com.google.android.gms:play-services-base:7.3.0'


    compile files('libs/Mavlink.jar')
}

assemble.dependsOn ':dependencyLibs:Mavlink:jar'

apply from: "${rootDir}/release_android_library.gradle"
buildscript {
    repositories {
        mavenCentral()
    }
}
repositories {
    mavenCentral()
}